#!/usr/bin/env zsh

asdf update

for plugin in $(asdf plugin-list); do
    echo -n "$plugin: "
    current=${=$(asdf list $plugin | tail -n 1)}
    echo -n "current $current "
    versions=(${=$(asdf list-all $plugin)})
    last=$versions[-1]
    echo -n "last $last "
    if [[ "$current" == "$last" ]]; then
        echo "→ no update"
        continue
    else
        selected=$(printf '%s\n' "${versions[@]}" | tac | fzf --layout=reverse --no-sort --header "Select $plugin version (current is $current) to use (or press ESC to skip):")
        if [[ -z "$selected" || "$current" == "$selected" ]]; then
            echo "→ keep"
            continue
        fi
        echo "→ update"
        preinstalled=(${=$(asdf list $plugin)})
        echo "Installing $plugin $selected..."
        asdf install $plugin $selected
        if [[ $? -eq 0 ]]; then
            echo "Setting $plugin $selected as global default version..."
            asdf global $plugin $selected
            for version in $preinstalled; do
                if [[ "$selected" != "$version" ]]; then
                    echo "Uninstalling old $plugin $version..."
                    asdf uninstall $plugin $version
                fi
            done
        else
            echo "Installation failed: not changing default version"
        fi
    fi
done

echo done
